plugins({
    id("com.palantir.git-version").version("0.12.3")
    id("jacoco")
    id("java")
    id("org.owasp.dependencycheck").version("6.5.0.1")
})

group = "jpass"
version = gitVersion()

repositories.mavenCentral()

dependencies({
    implementation("javax.xml.bind:jaxb-api:2.3.1")
    implementation("com.sun.xml.bind:jaxb-core:2.3.0.1")
    implementation("com.sun.xml.bind:jaxb-impl:2.3.1")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")
})

tasks.withType(JavaCompile).configureEach({
    options.encoding = "UTF-8"
    options.deprecation = true
    options.warnings = true
    options.compilerArgs << "-Xlint:all"
})

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

dependencyCheck({
    autoUpdate = false
    format = "ALL"
    outputDirectory = buildDir.toPath().resolve("reports").resolve("dependency-check").toString()
    analyzers.assemblyEnabled = false
    failBuildOnCVSS = 0
    suppressionFile = project.projectDir.toPath().resolve("src").resolve("main").resolve("resources").resolve("dependency-check-suppressions.xml")
})

jar({
    manifest({
        attributes("Main-Class": "jpass.JPass")
    })
})

task fatJar(type: Jar) {
    manifest {
        attributes "Main-Class": "jpass.JPass"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } with jar
}

test.useJUnitPlatform()
test.finalizedBy(jacocoTestReport)

jacocoTestReport.dependsOn(test)
jacocoTestReport.reports({
    xml.required.set(true)
})