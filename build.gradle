plugins({
    id("com.palantir.git-version").version("${gitVersionPluginVersion}")
    id("jacoco")
    id("java")
    id("org.owasp.dependencycheck").version("${owaspVersion}")
})

group = "jpass"
version = gitVersion()

repositories.mavenCentral()

dependencies({
    implementation("javax.xml.bind:jaxb-api:${jaxbApiVersion}")
    runtimeOnly("com.sun.xml.bind:jaxb-core:${jaxbCoreVersion}")
    runtimeOnly("com.sun.xml.bind:jaxb-impl:${jaxbImplVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${jupiterVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-params:${jupiterVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}")
})

tasks.withType(JavaCompile).configureEach({
    options.encoding = "UTF-8"
    options.deprecation = true
    options.warnings = true
    options.compilerArgs << "-Xlint:all"
})

java.sourceCompatibility = JavaVersion.VERSION_17
java.targetCompatibility = JavaVersion.VERSION_17

dependencyCheck({
    autoUpdate = false
    format = "ALL"
    outputDirectory = buildDir.toPath().resolve("reports").resolve("dependency-check").toString()
    analyzers.assemblyEnabled = false
    failBuildOnCVSS = 0
    suppressionFile = project.projectDir.toPath().resolve("src").resolve("main").resolve("resources").resolve("dependency-check-suppressions.xml")
})

jar({
    manifest({
        attributes("Main-Class": "jpass.JPass")
    })
})

task fatJar(type: Jar) {
    group = "build"
    manifest {
        attributes "Main-Class": "jpass.JPass"
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } with jar
}

test.useJUnitPlatform()
test.finalizedBy(jacocoTestReport)

jacocoTestReport.dependsOn(test)
jacocoTestReport.reports({
    xml.required.set(true)
})